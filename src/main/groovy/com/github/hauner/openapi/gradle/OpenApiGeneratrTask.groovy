/*
 * Copyright 2019 the original authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package com.github.hauner.openapi.gradle

import org.gradle.api.DefaultTask
import org.gradle.api.file.FileCollection
import org.gradle.api.tasks.Classpath
import org.gradle.api.tasks.InputDirectory
import org.gradle.api.tasks.Internal
import org.gradle.api.tasks.OutputDirectory
import org.gradle.api.tasks.TaskAction
import org.gradle.workers.IsolationMode
import org.gradle.workers.WorkerConfiguration
import org.gradle.workers.WorkerExecutor

import javax.inject.Inject

/**
 * generatr task. Uses a worker executor to run the generatr with an isolated "openapiGeneratr"
 * configuration classpath.
 *
 * @author Martin Hauner
 */
class OpenApiGeneratrTask extends DefaultTask {

    /**
     * Source directory (i.e. parent) of the openapi.yaml input file.
     *
     * Used by gradle for the up-to-date check.
     */
    @InputDirectory
    String apiDir

    /**
     * Target directory for the sources generated by the generatr.
     *
     * Used by gradle for the up-to-date check.
     */
    @OutputDirectory
    String targetDir

    /**
     * The dependencies of the generatr.
     */
    @Classpath
    FileCollection dependencies

    /**
     * name of the generatr.
     */
    @Internal
    String generatrName

    /**
     * properties configured by the generatr closure in the OpenApiGeneratrExtension.
     */
    @Internal
    Map<String, ?> generatrProps

    @Internal
    final WorkerExecutor workerExecutor

    @Inject
    OpenApiGeneratrTask (WorkerExecutor workerExecutor) {
        this.workerExecutor = workerExecutor
    }

    /**
     * runs the configured generator with its own classloader.
     */
    @TaskAction
    void runGeneratr () {
        workerExecutor.submit (OpenApiGeneratrWorker) { WorkerConfiguration cfg ->
            cfg.isolationMode = IsolationMode.CLASSLOADER
            cfg.classpath = dependencies.files
            cfg.params generatrName, generatrProps
        }

    }

}
