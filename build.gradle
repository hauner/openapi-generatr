plugins {
    id 'signing'
    id 'com.gradle.plugin-publish' version '1.2.0'
    id "com.github.ben-manes.versions" version "0.46.0"
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

group projectGroupId
version projectVersion

apply plugin: VersionPlugin
apply from: "${rootProject.rootDir}/gradle/publishing.gradle"

ext {
    api = '2023.1-SNAPSHOT'
}

allprojects {
    apply plugin: 'groovy'

    repositories {
        mavenCentral()

        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
            content {
               includeGroup "io.openapiprocessor"
            }
            mavenContent {
                snapshotsOnly()
            }
        }
    }

    dependencies {
        compileOnly "io.openapiprocessor:openapi-processor-api:${project.api}"
        implementation 'org.codehaus.groovy:groovy:3.0.17'
    }
}

java {
    withJavadocJar()
    withSourcesJar()

    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

testing {
    suites {
        test {
            useJUnitJupiter('5.9.2')
        }

        testInt(JvmTestSuite) {
            useJUnitJupiter('5.9.2')
            testType = TestSuiteType.INTEGRATION_TEST

            dependencies {
                implementation(project())
            }

            sources {
                java {
                    srcDirs = ['src/testInt/groovy']
                }
            }

            targets {
                configureEach {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

testInt {
    maxHeapSize = "2048M"
    systemProperty "PROJECT_DIR", "$projectDir"
}

tasks.named('check') {
    dependsOn(testing.suites.testInt)
}

testInt.dependsOn ':processor-v1:build'
testInt.dependsOn ':processor-one:build'
testInt.dependsOn ':processor-two:build'

dependencies {
    testImplementation platform ("org.spockframework:spock-bom:2.3-groovy-3.0")
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'net.bytebuddy:byte-buddy:1.14.5'
    testImplementation gradleTestKit()

    testIntImplementation platform ("org.spockframework:spock-bom:2.3-groovy-3.0")
    testIntImplementation 'org.spockframework:spock-core'
    testIntImplementation 'net.bytebuddy:byte-buddy:1.14.5'
    testIntImplementation gradleTestKit()
}

gradlePlugin {
    website = 'https://github.com/openapi-processor/openapi-processor-gradle'
    vcsUrl = 'https://github.com/openapi-processor/openapi-processor-gradle'

    plugins {
        processorPlugin {
            id = 'io.openapiprocessor.openapi-processor'
            displayName = 'Gradle openapi-processor plugin'
            description = 'plugin to run openapi-processor-*, e.g. openapi-processor-spring (requires gradle 7.0+, with gradle 5.5+ use 2021.3)'
            tags.set(['openapi', 'openapi-processor'])
            implementationClass = 'io.openapiprocessor.gradle.OpenApiProcessorPlugin'
        }
    }
}
