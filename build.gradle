plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.20.0'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
    id "com.github.ben-manes.versions" version "0.41.0"
}

apply plugin: VersionPlugin

group 'io.openapiprocessor'
version '2022.1'

ext {
    api = '2021.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

allprojects {
    apply plugin: 'groovy'

    repositories {
        mavenCentral()

        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
            content {
               includeGroup "io.openapiprocessor"
            }
            mavenContent {
                snapshotsOnly()
            }
        }
    }

    dependencies {
        compileOnly "io.openapiprocessor:openapi-processor-api:${project.api}"
        implementation 'org.codehaus.groovy:groovy:3.0.9'
    }
}

dependencies {
    testImplementation ('org.spockframework:spock-core:2.0-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation 'net.bytebuddy:byte-buddy:1.12.7'
}

tasks.withType(Test) {
    useJUnitPlatform()
}

testSets {
    testInt
}

check.dependsOn testInt
testInt.dependsOn ':processor-v1:build'
testInt.dependsOn ':processor-one:build'
testInt.dependsOn ':processor-two:build'

testInt {
    maxHeapSize = "1024M"
    systemProperty "PROJECT_DIR", "$projectDir"
}


gradlePlugin {
    plugins {
        processorPlugin {
            id = 'io.openapiprocessor.openapi-processor'
            displayName = 'Gradle openapi-processor plugin'
            description = 'plugin to run openapi-processor-*, e.g. openapi-processor-spring (requires gradle 7.0+, with gradle 5.5+ use 2021.3)'
            implementationClass = 'io.openapiprocessor.gradle.OpenApiProcessorPlugin'
        }
    }
}


pluginBundle {
    website = 'https://github.com/openapi-processor/openapi-processor-gradle'
    vcsUrl = 'https://github.com/openapi-processor/openapi-processor-gradle'
    tags = ['openapi', 'openapi-processor']
}

