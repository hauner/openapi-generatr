plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'org.unbroken-dome.test-sets' version '2.1.1'
}

group 'com.github.hauner.openapi'
version '1.0.0.B1'

targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/hauner/openapi-generatr"
    }
}

configurations {
    api
    implementation.extendsFrom api
}

dependencies {
    implementation localGroovy()

    // needed at runtime, see copyApi
    api 'com.github.hauner.openapi:openapi-generatr-api:1.0.0.B1'

    testImplementation ('org.spockframework:spock-core:1.3-groovy-2.5') {
        // avoid conflict with localGroovy()
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation 'net.bytebuddy:byte-buddy:1.9.13'
}


testSets {
    testInt
}

check.dependsOn testInt

// copy classes of api dependencies to build/classes so they are included in the plugin jar
task copyApi (type: Copy) {
    from {
        configurations.api.collect { zipTree (it) }
    }
    into ("$buildDir/classes/groovy/main")
    include ('**/*.class')
}

compileJava {
    compileJava.dependsOn ('copyApi')
}

//jar {
//    dependsOn (configurations.api)
//
//    with copySpec {
//      from {
//          configurations.api.collect { zipTree(it) }
//      }
//    }
//}

gradlePlugin {
    plugins {
        generatrPlugin {
            id = 'com.github.hauner.openapi-generatr'
            displayName = 'Gradle openapi-generatr plugin'
            description = 'plugin to run openapi-generatr-x generators, e.g. openapi-generatr-spring'
            implementationClass = 'com.github.hauner.openapi.gradle.OpenApiGeneratrPlugin'
        }
    }
}


pluginBundle {
    website = 'https://github.com/hauner/openapi-generatr-gradle'
    vcsUrl = 'https://github.com/hauner/openapi-generatr-gradle'
    tags = ['openapi']
}
