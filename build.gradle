plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
    id "com.github.ben-manes.versions" version "0.36.0"
}

group 'io.openapiprocessor'
version '2021.1'

ext {
    api = '2021.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

allprojects {
    apply plugin: 'groovy'

    repositories {
        mavenCentral()

        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
            content {
               includeGroup "io.openapiprocessor"
            }
            mavenContent {
                snapshotsOnly()
            }
        }

        // obsolete, releases are available from mavenCentral()
        maven {
            url "https://dl.bintray.com/openapi-processor/primary"
            content {
               includeGroup "io.openapiprocessor"
            }
            mavenContent {
                releasesOnly()
            }
        }

        // obsolete, snapshots are available from oss.sonatype.org
        maven {
            url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
            content {
               includeGroup "io.openapiprocessor"
            }
            mavenContent {
                snapshotsOnly()
            }
        }
    }

    dependencies {
        compileOnly "io.openapiprocessor:openapi-processor-api:${project.api}"
        implementation "org.codehaus.groovy:groovy:2.5.12"
    }
}

dependencies {
    testImplementation ('org.spockframework:spock-core:1.3-groovy-2.5') {
        // avoid conflict with explicit groovy version
        exclude group: 'org.codehaus.groovy'
    }

    testImplementation 'net.bytebuddy:byte-buddy:1.10.18'
}

sourceSets {
  main {
    java {
        srcDirs "${buildDir}/version"
    }
  }
}

compileJava.dependsOn "generateVersion"

testSets {
    testInt
}

check.dependsOn testInt
testInt.dependsOn ':processor-v1:build'
testInt.dependsOn ':processor-one:build'
testInt.dependsOn ':processor-two:build'

testInt {
    systemProperty "PROJECT_DIR", "$projectDir"
}


gradlePlugin {
    plugins {
        processorPlugin {
            id = 'io.openapiprocessor.openapi-processor'
            displayName = 'Gradle openapi-processor plugin'
            description = 'plugin to run openapi-processor-*, e.g. openapi-processor-spring (requires gradle 5.5+)'
            implementationClass = 'com.github.hauner.openapi.gradle.OpenApiProcessorPlugin'
        }
    }
}


pluginBundle {
    website = 'https://github.com/openapi-processor/openapi-processor-gradle'
    vcsUrl = 'https://github.com/openapi-processor/openapi-processor-gradle'
    tags = ['openapi', 'openapi-processor']
}

apply plugin: VersionPlugin
