plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'org.unbroken-dome.test-sets' version '2.1.1'
}

group 'com.github.hauner.openapi'
version '1.0.0.M6'

targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven {
        url  "https://dl.bintray.com/hauner/openapi-processor"
    }
}

dependencies {
    implementation localGroovy()

    compileOnly 'com.github.hauner.openapi:openapi-processor-api:1.0.0.M4'

    testImplementation ('org.spockframework:spock-core:1.3-groovy-2.5') {
        // avoid conflict with localGroovy()
        exclude group: 'org.codehaus.groovy'
    }

    testImplementation 'net.bytebuddy:byte-buddy:1.9.13'
}


testSets {
    testInt
}

check.dependsOn testInt
testInt.dependsOn ':processor-one:build'
testInt.dependsOn ':processor-two:build'

testInt {
    systemProperty "PROJECT_DIR", "$projectDir"
}


gradlePlugin {
    plugins {
        processorPlugin {
            id = 'com.github.hauner.openapi-processor'
            displayName = 'Gradle openapi-processor plugin'
            description = 'plugin to run openapi-processor-*, e.g. openapi-processor-spring (requires gradle 5.2+)'
            implementationClass = 'com.github.hauner.openapi.gradle.OpenApiProcessorPlugin'
        }
    }
}


pluginBundle {
    website = 'https://github.com/hauner/openapi-processor-gradle'
    vcsUrl = 'https://github.com/hauner/openapi-processor-gradle'
    tags = ['openapi']
}
