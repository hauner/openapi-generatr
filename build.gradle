plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
    id "com.github.ben-manes.versions" version "0.28.0"
}

group 'io.openapiprocessor'
version '1.0.0.M7'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

allprojects {
    apply plugin: 'groovy'

    repositories {
        mavenCentral()
        maven {
            url  "https://dl.bintray.com/openapi-processor/primary"
        }
    }

    dependencies {
        compileOnly 'io.openapiprocessor:openapi-processor-api:1.1.0.M1'
        implementation "org.codehaus.groovy:groovy:2.5.12"
    }
}

dependencies {
    testImplementation ('org.spockframework:spock-core:1.3-groovy-2.5') {
        // avoid conflict with explicit groovy version
        exclude group: 'org.codehaus.groovy'
    }

    testImplementation 'net.bytebuddy:byte-buddy:1.10.11'
}


testSets {
    testInt
}

check.dependsOn testInt
testInt.dependsOn ':processor-one:build'
testInt.dependsOn ':processor-two:build'

testInt {
    systemProperty "PROJECT_DIR", "$projectDir"
}


gradlePlugin {
    plugins {
        processorPlugin {
            id = 'com.github.hauner.openapi-processor'
            displayName = 'Gradle openapi-processor plugin'
            description = 'plugin to run openapi-processor-*, e.g. openapi-processor-spring (requires gradle 5.5+)'
            implementationClass = 'com.github.hauner.openapi.gradle.OpenApiProcessorPlugin'
        }
    }
}


pluginBundle {
    website = 'https://github.com/hauner/openapi-processor-gradle'
    vcsUrl = 'https://github.com/hauner/openapi-processor-gradle'
    tags = ['openapi']
}
